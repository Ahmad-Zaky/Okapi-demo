#!/usr/bin/perl -w

# Create linked table of contents from a Markdown file.
# Then include the output inside the original <FILE.md>

use strict;
use warnings;
use Getopt::Std;

my %opts = (l => 1, h => 99, s => 0);
if (!getopts('l:h:s:', \%opts) || @ARGV == 0) {
    print STDERR "\
Usage: md2toc [options] <FILE.md>\
        -l <lowest> specifies lowest heading level to include.\
        -h <highest> specifies highest heading level to include.\
        -s <skip> skip the first few lines of generated output.\n";
    exit 1;
}
my $lowest = $opts{l};
my $highest = $opts{h};
my $skip = $opts{s};

while (my $line = <>) {
    chomp $line;
    if ($line =~ s/^(#+) *//) {
        my $level = length($1);
        next if $level < $lowest || $level > $highest;
        my $link = lc($line);
        $link =~ s/ /-/g;
        $link =~ s/[^a-z0-9-]//g;
	if ($skip > 0) {
	    $skip--;
	} else {
	    print '    ' x ($level-$lowest), "* [$line](#$link)\n";
	}
    }
}
