#!/usr/bin/perl -w

# Create linked table of contents from a Markdown file.
# Then include the output inside the original <FILE.md>

use strict;
use warnings;

my $lowest = 1;
my $highest = 99;
my $skip = 0;

while (@ARGV > 2) {
    if ($ARGV[0] eq '-l') {
        $lowest = $ARGV[1]; shift; shift;
    } elsif ($ARGV[0] eq '-h') {
        $highest = $ARGV[1]; shift; shift;
    } elsif ($ARGV[0] eq '-s') {
        $skip = $ARGV[1]; shift; shift;
    } elsif ($ARGV[0] =~ /^-/) {
        print STDERR "Usage: md2toc [-l <lowest>] [-h <highest>] [-s <skip>] <FILE.md>\
        -l <lowest> specifies lowest heading level to include.\
        -h <highest> specifies highest heading level to include.\
        -s <skip> skip the first few lines of generated output.\n";
        exit 1;
    } else {
        last;
    }
}

while (my $line = <>) {
    chomp $line;
    if ($line =~ s/^(#+) *//) {
        my $level = length($1);
        next if $level < $lowest || $level > $highest;
        my $link = lc($line);
        $link =~ s/ /-/g;
        $link =~ s/[^a-z0-9-]//g;
	if ($skip > 0) {
	    $skip--;
	} else {
	    print '    ' x ($level-$lowest), "* [$line](#$link)\n";
	}
    }
}
